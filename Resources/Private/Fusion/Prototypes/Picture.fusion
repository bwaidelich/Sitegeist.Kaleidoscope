prototype(Sitegeist.Kaleidoscope:Picture) < prototype(Neos.Fusion:Component) {

	@styleguide {
		props {
			imageSource = Sitegeist.Kaleidoscope:DummyImageSource
			sources = Neos.Fusion:RawArray {
				1 = Neos.Fusion:RawArray {
					resolutions = ${[1, 1.5, 2]}
					media = 'screen and (min-width: 1600px)'
				}
				2 = Neos.Fusion:RawArray {
					widths = ${[320, 480, 800, 1000]}
					sizes = Neos.Fusion:RawArray {
						small = '(max-width: 320px) 280px'
						medium = '(max-width: 480px) 440px'
						large = '800px'
					}
					media = 'screen and (max-width: 1599px)'
				}
				3 = Neos.Fusion:RawArray {
					imageSource = Sitegeist.Kaleidoscope:DummyImageSource {
						text = "im am here for printing"
					}
					media = 'print'
				}
			}
			alt = 'Elva dressed as a fairy'
		}
	}

	imageSource = Sitegeist.Kaleidoscope:DummyImageSource
	sizes = null
	widths = null
	resolutions = null
	sources = null
	alt = null
	title = null

	renderer = afx`
		<picture @if.has={props.imageSource}>
			<Neos.Fusion:Collection collection={props.sources} itemName="source" @children="itemRenderer" @if.has={props.sources}>
				<source
					@if.hasResolutions={source.resolutions}
					@if.hasMedia={source.media}
					@context.subImageSource={source.imageSource ? source.imageSource : props.imageSource}
					srcset={subImageSource.resolutionSrcset(source.resolutions)}
					media={source.media}
				/>
				<source
					@if.hasSizesOrWidth={source.sizes || source.widths}
					@if.hasMedia={source.media}
					@context.subImageSource={source.imageSource ? source.imageSource : props.imageSource}
					srcset={subImageSource.widthSrcset(source.widths ? source.widths : Array.keys(source.sizes))}
					sizes={source.sizes ? Array.join(source.sizes, ", ") : '100vw'}
					media={source.media}
				/>
				<source
					@if.hasNeitherSourceNorSize={!source.sizes && !source.resolutions && !source.widths}
					@if.hasMedia={source.media}
					@context.subImageSource={source.imageSource ? source.imageSource : props.imageSource}
					srcset={subImageSource}
					media={source.media}
				/>
			</Neos.Fusion:Collection>
			<Sitegeist.Kaleidoscope:Image
				imageSource={props.imageSource}
				sizes={props.sizes}
				widths={props.widths}
				resolutions={props.resolutions}
				alt={props.alt}
				title={props.title}
			/>
		</picture>
	`
}