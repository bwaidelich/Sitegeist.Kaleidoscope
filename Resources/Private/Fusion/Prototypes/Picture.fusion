prototype(Sitegeist.Kaleidoscope:Picture) < prototype(Neos.Fusion:Component) {

    @styleguide {
        props {
            imageSource = Sitegeist.Kaleidoscope:DummyImageSource
            sources = Neos.Fusion:RawArray {
                1 = Neos.Fusion:RawArray {
                    srcset = '1x, 1.5x, 2x'
                    media = 'screen and (min-width: 1600px)'
                }
                2 = Neos.Fusion:RawArray {
                    imageSource = Sitegeist.Kaleidoscope:DummyImageSource {
                        text = "im am in webp format"
                        format = 'webp'
                    }
                    srcset = '320w, 480w, 800w, 1000w'
                    sizes = '(max-width: 320px) 280px, (max-width: 480px) 440px, 800px'
                    type = 'image/webp'
                    media = 'screen and (max-width: 1599px)'
                }
                3 = Neos.Fusion:RawArray {
                    imageSource = Sitegeist.Kaleidoscope:DummyImageSource {
                        text = "im am here for printing"
                    }
                    media = 'print'
                }
            }
            alt = 'Elva dressed as a fairy'
        }
    }

    imageSource = Sitegeist.Kaleidoscope:DummyImageSource
    srcset = null
    sizes = null
    sources = null
    formats = null
    alt = null
    title = null
    class = null
    content = ''
    dimensions = null

    @context {
        __imageSource = ${this.imageSource}
        __srcset = ${this.srcset}
        __sizes = ${this.sizes}
    }

    renderer = afx`
        <picture @if.has={props.imageSource} class={props.class}>
            {props.content}
            <Neos.Fusion:Collection collection={props.sources} itemName="source" @if.has={props.sources}>
                <Sitegeist.Kaleidoscope:Source
                    imageSource={source.imageSource ? source.imageSource : props.imageSource}
                    type={source.type}
                    media={source.media}
                    format={source.format}
                    srcset={source.srcset ? source.srcset : props.srcset}
                    sizes={source.sizes ? source.sizes : props.sizes}
                />
            </Neos.Fusion:Collection>
            <Neos.Fusion:Collection
                @if.has={props.imageSource && props.formats}
                collection={Type.isArray(props.formats) ? props.formats : String.split(props.formats, ',')}
                itemName="format"
            >
                <Sitegeist.Kaleidoscope:Source format={String.trim(format)} />
            </Neos.Fusion:Collection>
            <Sitegeist.Kaleidoscope:Image
                imageSource={props.imageSource}
                sizes={props.sizes}
                srcset={props.srcset}
                alt={props.alt}
                title={props.title}
                dimensions={props.formats && !props.content && !props.sources}
                dimensions.@override.fromProps={props.dimensions}
                dimensions.@override.fromProps.@if.isBoolean={Type.isBoolean(props.dimensions)}
            />
        </picture>
    `
}
