prototype(Sitegeist.Kaleidoscope:Picture) < prototype(Neos.Fusion:Component) {

	@styleguide {
		props {
			imageSource = Sitegeist.Kaleidoscope:DummyImageSource
			sources = Neos.Fusion:RawArray {
				1 = Neos.Fusion:RawArray {
					resolutions = ${[1, 1.5, 2]}
					media = 'screen and (min-width: 1600px)'
				}
				2 = Neos.Fusion:RawArray {
					sizes = Neos.Fusion:RawArray {
						320 = '(max-width: 320px) 280px'
						480 = '(max-width: 480px) 440px'
						800 = '800px'
					}
					media = 'screen and (max-width: 1599px)'
				}
				3 = Neos.Fusion:RawArray {
					imageSource = Sitegeist.Kaleidoscope:DummyImageSource {
						text = "im am here for printing"
					}
					media = 'print'
				}
			}
			alt = 'Elva dressed as a fairy'
		}
	}

	imageSource = Sitegeist.Kaleidoscope:DummyImageSource
	sources = null
	alt = null
	title = null

	renderer = afx`
		<picture>
			<Neos.Fusion:Collection collection={props.sources} itemName="source" @children="itemRenderer" @if.has={props.sources}>
				<source
					@if.hasResolutions={source.resolutions}
					@if.hasMedia={source.media}
					@context.subImageSource={source.imageSource ? source.imageSource : props.imageSource}
					srcset={subImageSource.resolutionSrcset(source.resolutions)}
					media={source.media}
				/>
				<source
					@if.hasSizes={source.sizes}
					@if.hasMedia={source.media}
					@context.subImageSource={source.imageSource ? source.imageSource : props.imageSource}
					srcset={subImageSource.widthSrcset(Array.keys(source.sizes))}
					sizes={Array.join(source.sizes, ", ")}
					media={source.media}
				/>
				<source
					@if.hasNeitherSourceNorSize={!source.sizes && !source.resolutions}
					@if.hasMedia={source.media}
					@context.subImageSource={source.imageSource ? source.imageSource : props.imageSource}
					srcset={subImageSource}
					media={source.media}
				/>
			</Neos.Fusion:Collection>
			<img
				src={props.imageSource}
				alt={props.alt}
				title={props.title}
			/>
		</picture>
	`
}