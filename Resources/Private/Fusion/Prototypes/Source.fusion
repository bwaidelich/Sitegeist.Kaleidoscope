prototype(Sitegeist.Kaleidoscope:Source) < prototype(Neos.Fusion:Component) {

    @propTypes {
        imageSource = ${PropTypes.instanceOf('\\Sitegeist\\Kaleidoscope\\EelHelpers\\ImageSourceHelperInterface')}
    }

    imageSource = ${__imageSource}
    srcset = ${__srcset}
    sizes = ${__sizes}
    type = null
    media = null
    format = null

    renderer = Neos.Fusion:Component {
        imageSource = ${props.format ? props.imageSource.setFormat(props.format) : props.imageSource}
        type = ${props.format ? 'image/' + props.format : props.type}
        srcset = ${props.srcset}
        sizes = ${props.sizes}
        media = ${props.media}

        renderer = afx`
            <source @if.has={props.imageSource}
                srcset={props.imageSource.srcset(props.srcset)}
                srcset.@if.has={props.srcset}
                sizes={props.sizes}
                sizes.@process.join={Type.isArray(value) ? Array.join(value, ', ') : value}
                type={props.type}
                media={props.media}
            />
        `
    }
}
