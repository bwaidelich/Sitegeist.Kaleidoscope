prototype(Sitegeist.Kaleidoscope:Image) < prototype(Neos.Fusion:Component) {

	@styleguide {
		props {
			imageSource = Sitegeist.Kaleidoscope:DummyImageSource
			alt = 'Elva dressed as a fairy'
		}

		propSets {
			multires {
				resolutions = ${[1, 1.5, 2]}
			}
			multisize {
				widths = ${[320, 400, 600, 800, 1000, 1200, 1600]}
				sizes = Neos.Fusion:RawArray {
					large = '(min-width: 800px) 1000px'
					medium = '(min-width: 480px) 800px'
					small = '(min-width: 320px) 440px'
					default = '100vw'
				}
			}
			multisize_sizeOnly {
				sizes = Neos.Fusion:RawArray {
					1000 = '(min-width: 800px) 1000px'
					800 = '(min-width: 480px) 800px'
					440 = '(min-width: 320px) 440px'
					280 = '100vw'
				}
			}
			multisize_widthOnly {
				widths = ${[320, 400, 600, 800, 1000, 1200, 1600]}
			}
		}
	}

	@propTypes {
		imageSource = ${PropTypes.instanceOf('\\Sitegeist\\Kaleidoscope\\EelHelpers\\ImageSourceHelperInterface')}
	}

	imageSource = null
	sizes = null
	widths = null
	resolutions = null
	alt = null
	title = null

	renderer = afx`
		<img @if.has={props.imageSource}
			src={props.imageSource}
			srcset={null}
			srcset.@process.resolutionSrcset={props.resolutions ? props.imageSource.resolutionSrcset(props.resolutions) : value}
			srcset.@process.widthSrcset={props.widths ? props.imageSource.widthSrcset(props.widths) : value}
			sizes={props.sizes}
			sizes.@process.join={Type.isArray(value) ? Array.join(value, ', ') : value}
			alt={props.alt}
			title={props.title}
		/>
	`
}